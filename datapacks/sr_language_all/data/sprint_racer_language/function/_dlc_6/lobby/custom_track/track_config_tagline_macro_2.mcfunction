#executed by @e[tag=focusT,type=armor_stand]
#with storage sprint_racer:custom_track_storage_$(id)

$execute if entity @s[tag=!useTagLine] run tellraw @a ["",{translate:"sr.custom_track.creator_text",color:"white",bold:true},{text:" "},{translate:"sr.text_menu.on",color:"gray",click_event:{action:"run_command",command:"/function sprint_racer:game_logic/10/custom_config/generic_enable_macro {tag:\"useTagLine\",page:\"\"}"},hover_event:{action:"show_text",value:[{translate:"sr.basic.on"}]}},{text:"  "},{translate:"sr.text_menu.off",color:"yellow",click_event:{action:"run_command",command:"/function sprint_racer:game_logic/10/custom_config/generic_disable_macro {tag:\"useTagLine\",page:\"\"}"},hover_event:{action:"show_text",value:[{translate:"sr.basic.off"}]}},{text:"  "},{translate:"sr.custom_track.set",color:"light_purple",click_event:{action:"suggest_command",command:"/data modify storage sprint_racer:custom_track_storage_$(storage_id) tagline set value [{text:\"Created by: \",bold:false},{text:\"YOURNAME\",color:\"yellow\"}]"},hover_event:{action:"show_text",value:"Set creator name (replace \"YOURNAME\")"}}]
$execute if entity @s[tag=useTagLine] run tellraw @a ["",{translate:"sr.custom_track.creator_text",color:"white",bold:true},{text:" "},{translate:"sr.text_menu.on",color:"yellow",click_event:{action:"run_command",command:"/function sprint_racer:game_logic/10/custom_config/generic_enable_macro {tag:\"useTagLine\",page:\"\"}"},hover_event:{action:"show_text",value:[{translate:"sr.basic.on"}]}},{text:"  "},{translate:"sr.text_menu.off",color:"gray",click_event:{action:"run_command",command:"/function sprint_racer:game_logic/10/custom_config/generic_disable_macro {tag:\"useTagLine\",page:\"\"}"},hover_event:{action:"show_text",value:[{translate:"sr.basic.off"}]}},{text:"  "},{translate:"sr.custom_track.set",color:"light_purple",click_event:{action:"suggest_command",command:"/data modify storage sprint_racer:custom_track_storage_$(storage_id) tagline set value [{text:\"Created by: \",bold:false},{text:\"YOURNAME\",color:\"yellow\"}]"},hover_event:{action:"show_text",value:"Set creator name (replace \"YOURNAME\")"}}]

$data modify storage sprint_racer:func_args text_component set value $(tagline)
execute if entity @s[tag=!useTagLine] run tellraw @a ["",{text:" x ",color:"dark_gray"},{nbt:"text_component",storage:"sprint_racer:func_args",interpret:true}]
execute if entity @s[tag=useTagLine] run tellraw @a ["",{text:" > ",color:"yellow"},{nbt:"text_component",storage:"sprint_racer:func_args",interpret:true}]